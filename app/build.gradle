plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'rijksstudio-test-convention'
    id 'rijksstudio-spotless-convention'
}

def versionPropertiesFile = project.file('version.properties')
def versionProperties = new Properties()
versionPropertiesFile.withInputStream { versionProperties.load(it) }

android {
    namespace 'com.kyrillosg.rijksstudio'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "com.kyrillosg.rijksstudio"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()

        def appVersionCode = versionProperties['versionCode'].toInteger()
        versionName getAppVersionName(appVersionCode)
        versionCode appVersionCode

        testInstrumentationRunner = "com.kyrillosg.rijksstudio.InstrumentationTestRunner"
        testInstrumentationRunnerArguments["runnerBuilder"] = "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }

    buildTypes {
        def fallback = '"APP_CENTER_SECRET_NOT_DEFINED"'

        debug {
            buildConfigField "String", "APP_CENTER_SECRET", localProperties['APP_CENTER_SECRET_DEBUG'] ?: fallback
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "APP_CENTER_SECRET", localProperties['APP_CENTER_SECRET_RELEASE'] ?: fallback
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "rijksstudio-${variant.baseName}-${variant.versionCode}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }

    testOptions {
        animationsDisabled true

        managedDevices {
            devices {
                // Run instrumentation tests on this device with ./gradlew pixel2api30DebugAndroidTest
                pixel2api30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 30
                    systemImageSource = "aosp"
                }
            }
        }
    }
}

dependencies {
    implementation(project(":core:di"))
    implementation(project(":feature:collection"))

    implementation(libs.bundles.kotlinx.coroutines)
    implementation(libs.koin.android)

    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.navigation.fragment.ktx)
    implementation(libs.androidx.navigation.ui.ktx)

    implementation(libs.appcenter.analytics)
    implementation(libs.appcenter.crashes)
    implementation(libs.material)
    implementation(libs.napier)

    testImplementation(libs.bundles.ktor)
    testImplementation(libs.koin.test.junit5)

    androidTestImplementation(project(":core:ui"))
    androidTestImplementation(libs.bundles.instrumentation.testing)
    androidTestRuntimeOnly(libs.junit5.android.test.runner)
    debugImplementation(libs.androidx.fragment.testing.manifest)
}

private static String getAppVersionName(Integer versionCode) {
    def yearAndMonth = new Date().format("yyyy.MM", TimeZone.getTimeZone('UTC'))
    return "$yearAndMonth.$versionCode"
}

task incrementVersionCode {
    doLast {
        def currentVersionCode = versionProperties['versionCode'].toInteger()
        def nextVersionCode = currentVersionCode + 1
        versionProperties.setProperty('versionCode', nextVersionCode.toString())
        versionPropertiesFile.withWriter { versionProperties.store(it, null) }

        println("Version code bumped from $currentVersionCode to $nextVersionCode")
    }
}

task printVersionName {
    doLast {
        print android.defaultConfig.versionName
    }
}
